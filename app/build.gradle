import java.text.SimpleDateFormat

apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

apply plugin: 'kotlin-kapt'

// 加载签名配置文件
Properties props = new Properties()
props.load(new FileInputStream(file("signing.properties")))

// app 名称
static String appName() {
    return "Jeecg"
}
// 生产时间
static String releaseTime() {
    SimpleDateFormat format = new SimpleDateFormat("dd_HH_mm", Locale.CHINA)
    return format.format(new Date())
}

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    defaultConfig {
        applicationId "com.mazaiting.easydemo"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    // 签名配置
    signingConfigs {
        release {
            keyAlias props['KEY_ALIAS']
            keyPassword props['KEY_PASSWORD']
            storeFile file(props['KEYSTORE_FILE'])
            storePassword props['KEYSTORE_PASSWORD']
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    applicationVariants.all {
        variant ->
            //这个修改输出的APK路径
            if (variant.buildType.name != "debug") {//防止AS无法安装debug包(apk)
                variant.getPackageApplication().outputDirectory = new File(project.rootDir.absolutePath + "/apk")
            }

            variant.getPackageApplication().outputScope.apkDatas.forEach {
                apkData ->
                    //这个修改输出APK的文件名
                    apkData.outputFileName =
                            "${appName()}" + "_" + "${releaseTime()}" + "_" +
                                    variant.versionName.replace(".", "_") + ".apk"
            }
    }

    lintOptions {
        abortOnError false
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    // Easy 框架库
    implementation project(':easy')
    // dagger 注解
    kapt "com.google.dagger:dagger-compiler:$dagger_version"
    // room 编译工具
    kapt "androidx.room:room-compiler:$room_version"
}
apply from: "./../kotlin.gradle"
